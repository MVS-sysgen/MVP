//SYSTEM$  JOB (SYS),'INSTALL SYSTEM CMD',CLASS=S,
//     USER=IBMUSER,PASSWORD=SYS1
//*------------------------------------------------------------*
//*        INSTALL OF THE 'SYSTEM' TSO CP.  THIS COMMAND GIVES *
//*        YOU THE FOLLOWING INFORMATION:                      *
//*        SYSTEM RELEASE,CPU TYPE,SERIAL NUMBER,CORE AMOUNT,  *
//*        IPL VOLUME, AND DATE IPL'D.     ( NEAT )            *
//*------------------------------------------------------------*
//SYSTEM  EXEC ASMFCL,PARM.ASM='RENT,OBJ,NODECK',
//        PARM.LKED='RENT,MAP,LIST,XREF',MAC1='SYS1.AMODGEN'
//ASM.SYSIN  DD *
         MACRO , JVDENTER - V1M00 - 01/28/74
&LABEL   JVDENTER                                                      +
               &BASE=(11),                                             +
               &REGS=(14,12),                                          +
               &ID=,                                                   +
               &VERSION=,                                              +
               &DSA=72,                                                +
               &ORIGIN=*,                                              +
               &CFL=YES
.*
         LCLC  &EPID,&BASR,&START
.*
.BEGIN   AIF   ('&LABEL' EQ '').IDT
.*
         SPACE
&LABEL   DS    0H
         ENTRY &LABEL
         SPACE
.*
.IDT     AIF   (T'&ID EQ 'O').IDD
&EPID    SETC  '&ID'
         AGO   .VER
.IDD     AIF   ('&LABEL' EQ '').IDC
&EPID    SETC  '&LABEL'
         AGO   .VER
.IDC     ANOP
&EPID    SETC  '&SYSECT'
.VER     AIF   (T'&VERSION EQ 'O').GEN
&EPID    SETC  '&EPID-&VERSION-&SYSDATE-&SYSTIME'
         MNOTE *,'       &EPID'
.*
.GEN     ANOP
&BASR    SETC  '&BASE(1)'
.*
&START   SETC  '&ORIGIN'
         AIF   ('&ORIGIN' NE '@').START
&START   SETC  '&SYSECT'
.START   AIF   ('&ORIGIN' EQ '*').HERE
.*
         USING &START,&BASR
         SAVE  &REGS,,&EPID
         BALR  &BASR,0
         LA    14,*-&ORIGIN
         SLR   &BASR,14
         LR    2,13
         SPACE
.*
         AGO   .GETDSA
.HERE    ANOP
.*
         USING &START,&BASR
         SAVE  &REGS,,&EPID
         LR    &BASR,15
         LR    2,13
         SPACE
.*
.GETDSA  AIF   ('&DSA' EQ '0').NOCFL
.*
         LA    3,&DSA
         GETMAIN R,LV=(3)
         ST    3,0(1)
         ST    13,4(1)
         LR    13,1
.*
         AIF   ('&CFL' EQ 'NO').NOCFL
.*
         ST    1,8(2)
.*
.NOCFL   ANOP
.*
         LM    14,04,12(2)
         SPACE
.*
         MEND
         MACRO , JVDLEAVE - V1M00 - 01/28/74
&LABEL   JVDLEAVE                                                      +
               &REGS=(14,12),                                          +
               &RC=,                                                   +
               &DSA=
.*
         LCLC  &L
.*
         AIF   ('&DSA' EQ '0').NODSA
.*
&LABEL   LH    0,2(13)
         LR    1,13
         L     13,4(13)
.*
         AIF   ('&RC' NE '(15)').FREE
.*
         LR    2,15
.*
.FREE    ANOP
.*
         FREEMAIN R,LV=(0),A=(1)
.*
.RCCHK   AIF   ('&RC' EQ '').NORC
         AIF   ('&RC' NE '(15)').RTRN
.*
         LR    15,2
.*
         AGO   .RTRNN
.RTRN    AIF   ('&RC'(1,1) NE '(').RTRNC
.*
         LR    15,&RC(1)
.*
.RTRNN   ANOP
.*
&L       RETURN &REGS,RC=(15)
         SPACE
.*
         MEXIT
.RTRNC   ANOP
.*
&L       RETURN &REGS,RC=&RC
         SPACE
.*
         MEXIT
.NORC    ANOP
.*
&L       RETURN &REGS
         SPACE
.*
         MEXIT
.NODSA   ANOP
&L       SETC  '&LABEL'
         AGO   .RCCHK
.*
         MEND
         MACRO
&L       JVDEQU &O
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
.*
         MEND
 TITLE 'S Y S T E M   C O N F I G U R A T I O N   D I S P L A Y'
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                                                                     *
*  SYSMVS IS THE OS/MVS VERSION OF THE SYSTEM CONFIGURATION DISPLAY   *
*     COMMAND.  IT SHOWS A TSO USER THE OPERATING SYSTEM, RELEASE,    *
*     CPU MODEL, SERIAL NUMBER(S), AND MAIN STORAGE SIZE, AND DATE.   *
*                                                                     *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
         SPACE 2
SYSMVS   CSECT
         SPACE
         JVDENTER DSA=(DSAEND-DSAWRK),VERSION=&SYSPARM
         USING    DSAWRK,R13
         EJECT
         L     R10,CVTPTR
         USING CVT,R10
         L     R3,CVTSYSAD
         LA    R5,0(R10)
         S     R5,F8
         MVC   BUFFER(BUFEND-BUFFER),MASK
         MVC   BRLSE,4(R5)
         MVC   BLEVL,6(R5)
         MVI   BMODL,C'-'
         LA    R15,16
         IC    R14,CVTDCB
         N     R14,VSMASK
         SRL   R14,1
         L     R9,CVTEORM
         SRA   R9,10
         LA    R9,1(R9)
         CVD   R9,DWD
         ED    BCORE,DWD+5
         MVC   BIPLV,UCBVOLI-UCBOB(R3)
         ED    BDATE,CVTDATE+1
         L     R4,CVTPCCAT
         L     R4,0(R4)      UNIPROCESSOR ONLY !!!!!!!!!
         MVC   BSNUM,4+2(R4) PCCACPID
         MVC   BMODL+1(3),4+9(R4)
         EJECT
         LA    R7,BUFEND-BUFFER
         TPUT  BUFFER,(R7)
         SPACE
         JVDLEAVE
         EJECT
VSMASK   DC    F'2'
F8       DC    F'8'
MASK     DC    C'MVS R03.0A  S/370-168  #60116'
         DC    X'402020202120',C'K  '
MASKC    DC    C'IPL''D FROM DLIB36 ON 76.079'
         ORG   MASKC+L'MASKC-6
         DC    X'2120',C'.',X'202020'
         LTORG
         EJECT
DSAWRK   DSECT
SSA      DS   18F
BUFFER   EQU  *
BTYPE    DS   CL5
BRLSE    DS   CL2
         DS   C'.'
BLEVL    DS   CL2
         DS   C'  S/370'
BMODL    DS   CL4
         DS   C'  '
BSNUM    DS    CL6
BCORE    DS    ZL6
         DS    C'K  IPL''D FROM '
BIPLV    DS    CL6
         DS    C' ON '
BDATE    DS    C'74.305'
BUFEND   EQU   *
DWD      DS    D
DSAEND   EQU   *
         SPACE
         JVDEQU
         EJECT
         EJECT
CVT      DSECT
         CVT   LIST=YES
         EJECT
         IEFUCBOB
         SPACE 2
         END   SYSMVS
//LKED.SYSLMOD DD DSN=SYS2.CMDLIB,DISP=SHR
 NAME SYSTEM(R)
/*
//HELP    EXEC PGM=IEBUPDTE,PARM=NEW,COND=(0,NE)
//SYSPRINT DD  SYSOUT=*
//SYSUT2   DD  DISP=SHR,DSN=SYS2.HELP
//SYSIN    DD  *
./ ADD NAME=SYSTEM
)F FUNCTION -                                                           01365470
  THE SYSTEM COMMAND DISPLAYS THE STATUS OF THE SYSTEM.                 01365480
)X SYNTAX -                                                             01365490
      SYSTEM                                                            01365500
)O OPERANDS -                                                           01365510
  THERE ARE NO OPERANDS FOR THIS COMMAND.                               01365520
                                                                        01365530
  ITEMS RETURNED ARE AS FOLLOW:                                         01365540
  SYSTEM RLSE, CPU MODEL, CPU ID, MEMORY ONLINE (AMOUNT), IPL           01365550
  VOLUME, AND DATE IPL'D.                                               01365560
./ ENDUP
/*
